import org.parchmentmc.compass.tasks.GenerateExport

import java.time.OffsetDateTime
import java.time.ZoneOffset
import java.time.format.DateTimeFormatter

plugins {
    id 'org.parchmentmc.compass' version '0.2.+'
    id 'org.parchmentmc.writtenbooks' version '0.+'
    id 'maven-publish'
}

group = 'org.parchmentmc.data'
def releaseType = project.findProperty('releaseType') ?: "local"
def date = OffsetDateTime.now(ZoneOffset.UTC).format(DateTimeFormatter.ofPattern('yyyyMMdd'))
def isRelease = releaseType == "release"
version = date + (isRelease ? '' : "-${releaseType}-SNAPSHOT")
println("Version: $version")

writtenbooks {
    snapshotVersion = !isRelease
    githubRepo = 'ParchmentMC/Parchment'
}

compass {
    version = '1.17'
}

repositories {
    maven {
        name = 'MinecraftForge'
        url = 'https://maven.minecraftforge.net/'
    }
}

dependencies {
    // mcpconfig 'de.oceanlabs.mcp:mcp_config:1.16.5-20210303.130956'
}

tasks.register('officialExportZip', Zip) {
    group = "build"
    description = "Creates a ZIP archive containing the export produced by the 'official' intermediate provider and production data."
    from(tasks.named('generateOfficialExport', GenerateExport).flatMap { it.output })
    rename { 'parchment.json' }
    reproducibleFileOrder = true
    preserveFileTimestamps = false
    destinationDirectory = project.layout.buildDirectory.dir('exportZips')
    archiveBaseName = 'officialExport'
}

tasks.register('officialStagingExportZip', Zip) {
    group = "build"
    description = "Creates a ZIP archive containing the export produced by the 'official' intermediate provider and staging data."
    from(tasks.named('generateOfficialStagingExport', GenerateExport).flatMap { it.output })
    rename { 'parchment.json' }
    reproducibleFileOrder = true
    preserveFileTimestamps = false
    destinationDirectory = project.layout.buildDirectory.dir('exportZips')
    archiveBaseName = 'officialStagingExport'
}

publishing {
    publications.all { p ->
        p.pom {
            name = 'Parchment Mappings'
            description = 'Parameter names and javadoc mappings for Minecraft: Java Edition.'
            organization {
                name = 'ParchmentMC'
                url = 'https://github.com/ParchmentMC'
            }
            licenses {
                license {
                    name = "CC0-1.0"
                    url = "https://creativecommons.org/publicdomain/zero/1.0/legalcode"
                }
            }
        }
        afterEvaluate {
            p.artifactId = "parchment-${project.compass.version.get()}"
            p.pom.properties['minecraft_version'] = project.compass.version
        }
    }
    publications.create('export', MavenPublication) { p ->
        p.artifact tasks.named("officialExportZip", Zip)
    }
    publications.create('staging', MavenPublication) { p ->
        p.artifact tasks.named("officialStagingExportZip", Zip)
        p.version = "staging-SNAPSHOT"
    }
    repositories {
        maven {
            name 'projectLocal'
            url "file://${rootProject.file('repo').absolutePath}"
        }
    }
}

tasks.register("publishExport") {
    group = PublishingPlugin.PUBLISH_TASK_GROUP
    description = "Publishes the 'export' Maven publication."

    dependsOn tasks.withType(PublishToMavenRepository).matching {
        it.publication == publishing.publications.export
    }
}
